!+ interpolate the x interpolants to the y value of the departure point
!+ $Id: heryin.f90,v 1.5 1999/08/27 17:00:23 m214030 Exp $

SUBROUTINE heryin(pf,fint,fyb,fyt,y,dy,ydp,jdp,fdp)
 
  ! Description:
  ! 
  ! Interpolate the x interpolants to the y value of the departure point
  ! 
  ! Method:
  ! 
  ! For each departure point in the latitude slice to be forecasted,
  ! interpolate (using unequally spaced Hermite cubic formulas) the
  ! x interpolants to the y value of the departure point.
  !
  ! Authors:
  !
  ! Original version:  J. Olson
  ! Standardized:      J. Rosinski, June 1992
  ! Reviewed:          D. Williamson, P. Rasch, August 1992
  ! Modified:          U. Schlese,  DKRZ - Hamburg, May 1994
  ! f90 version:       L. Kornblueh, U. Schulzweida, May 1998
  ! 
  ! for more details see file AUTHORS
  !

  USE mo_grid, ONLY: platd, pgls
  USE mo_slt,  ONLY: ppdy

  IMPLICIT NONE

  !  Scalar arguments with intent(In):
  INTEGER, INTENT (IN) :: pf

  !  Array arguments with intent(In):
  REAL, INTENT (IN) :: dy(platd), fint(pgls,ppdy,pf), fyb(pgls,pf), &
&                      fyt(pgls,pf), y(platd), ydp(pgls)
  INTEGER, INTENT (IN) :: jdp(pgls)

  !  Array arguments with intent(Out):
  REAL, INTENT (OUT) ::  fdp(pgls,pf)

  ! pf      Number of fields being interpolated.
  ! fint    (fint(i,k,j,m),j=ppdy/2,ppdy/2 + 1) contains the x
  !         interpolants at the endpoints of the y-interval that
  !         contains the departure point for grid point (i,k).  The last
  !         index of fint allows for interpolation of multiple fields.
  !         fint is generated by a call to herxin.
  ! fyb     fyb(i,k,.) is the derivative at the "bottom" of the
  !         y-interval that contains the departure point of grid
  !         point (i,k).  fyb is generated by a call to cubydr.
  ! fyt     fyt(i,k,.) is the derivative at the "top" of the y-interval
  !         that contains the departure point of grid point (i,k).
  !         fyt is generated by a call to cubydr.
  ! y       y-coordinate (latitude) values in the extended array.
  ! dy      Increment in the y-coordinate value for each interval in the
  !         extended array.
  ! ydp     ydp(i,k) is the y-coordinate of the departure point that
  !         corresponds to global grid point (i,k) in the latitude slice
  !         being forecasted.
  ! jdp     jdp(i,k) is the index of the y-interval that contains the
  !         departure point corresponding to global grid point (i,k) in
  !         the latitude slice being forecasted.
  !         Note that
  !         y(jdp(i,k)) .le. ydp(i,k) .lt. y(jdp(i,k)+1) .
  ! fdp     Horizontally interpolated field values at the departure point
  !         for the latitude slice being forecasted.

  !  Local scalars: 
  ! latitude interval containing dep. pt.
  REAL :: dyj, yb, yt
  INTEGER :: i, jb, jt, m

  !  Local arrays: 
  ! interpolation coefficients
  REAL :: dhb(pgls), dht(pgls), hb(pgls), ht(pgls)


  !  Executable statements 
  jb = ppdy/2
  jt = jb + 1

  DO i = 1, pgls
    dyj = dy(jdp(i))
    yb = (y(jdp(i)+1)-ydp(i))/dyj
    yt = 1. - yb
    hb(i) = (3.0-2.0*yb)*yb**2
    ht(i) = (3.0-2.0*yt)*yt**2
    dhb(i) = -dyj*(yb-1.)*yb**2
    dht(i) = dyj*(yt-1.)*yt**2
  END DO

  ! Loop over fields.

  DO m = 1, pf
    DO i = 1, pgls
      fdp(i,m) = fint(i,jb,m)*hb(i) + fyb(i,m)*dhb(i) + fint(i,jt,m)*ht(i) + &
&          fyt(i,m)*dht(i)
    END DO
  END DO

  RETURN
END SUBROUTINE heryin
