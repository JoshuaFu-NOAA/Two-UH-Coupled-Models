!+ interpolate the x interpolants to the y value of the departure point
!+ $Id: lagyin.f90,v 1.5 1999/08/27 17:00:27 m214030 Exp $

SUBROUTINE lagyin(pf,fint,wdy,ydp,jdp,fdp)

  ! Description:
  !
  ! Interpolate the x interpolants to the y value of the departure point
  !
  ! Mehotd:
  !
  ! For each departure point in the latitude slice to be forecasted,
  ! interpolate (using unequally spaced Lagrange cubic formulas) the
  ! x interpolants to the y value of the departure point.
  !
  ! Authors:
  !
  ! Standardized:      J. Rosinski, June 1992
  ! Reviewed:          D. Williamson, P. Rasch, August 1992
  ! f90 version:       L. Kornblueh, U. Schulzweida, May 1998
  ! 
  ! for more details see file AUTHORS
  !

  USE mo_exception, ONLY: finish
  USE mo_grid,      ONLY: pgls, platd
  USE mo_slt,       ONLY: ppdy
  USE mo_doctor,    ONLY: nout

  IMPLICIT NONE

  !  Scalar arguments with intent(In):
  INTEGER :: pf

  !  Array arguments with intent(In):
  REAL, INTENT (IN) :: fint(pgls,ppdy,pf), wdy(4,2,platd), ydp(pgls)
  INTEGER, INTENT (IN) :: jdp(pgls)

  !  Array arguments with intent(Out):
  REAL, INTENT (OUT) :: fdp(pgls,pf)

  ! pf      Number of fields being interpolated.
  ! fint    (fint(i,k,j,m),j=ppdy/2,ppdy/2 + 1) contains the x
  !         interpolants at the endpoints of the y-interval that contains
  !         the departure point for grid point (i,k).  The last index of
  !         fint allows for interpolation of multiple fields.  fint is
  !         generated by a call to herxin.
  ! wdy     Grid values and weights for Lagrange cubic interpolation on
  !         the unequally spaced y-grid.
  ! ydp     ydp(i,k) is the y-coordinate of the departure point that
  !         corresponds to global grid point (i,k) in the latitude slice
  !         being forecasted.
  ! jdp     jdp(i,k) is the index of the y-interval that contains the
  !         departure point corresponding to global grid point (i,k) in
  !         the latitude slice being forecasted.
  !         Note that
  !         y(jdp(i,k)) .le. ydp(i,k) .lt. y(jdp(i,k)+1) .
  ! fdp     Horizontally interpolated field values at the departure point
  !         for the latitude slice being forecasted.

  !  Local scalars: 
  ! -- interpolation weights/coeffs.
  REAL :: coef12, coef34, ymy1, ymy2, ymy3, ymy4
  INTEGER :: i, m 

  !  Local arrays: 
  REAL :: term1(pgls), term2(pgls), term3(pgls), term4(pgls)


  !  Executable statements 

  IF (ppdy/=4) THEN
    WRITE (nout,'(A)') 'LAGYIN:Error:  ppdy .ne. 4'
    CALL finish('lagyin','Run terminated.')
  END IF

  DO i = 1, pgls
    ymy3 = ydp(i) - wdy(3,1,jdp(i))
    ymy4 = ydp(i) - wdy(4,1,jdp(i))
    coef12 = ymy3*ymy4
    ymy2 = ydp(i) - wdy(2,1,jdp(i))
    term1(i) = coef12*ymy2*wdy(1,2,jdp(i))
    ymy1 = ydp(i) - wdy(1,1,jdp(i))
    term2(i) = coef12*ymy1*wdy(2,2,jdp(i))
    coef34 = ymy1*ymy2
    term3(i) = coef34*ymy4*wdy(3,2,jdp(i))
    term4(i) = coef34*ymy3*wdy(4,2,jdp(i))
  END DO

  ! Loop over fields.

  DO m = 1, pf
    DO i = 1, pgls
      fdp(i,m) = fint(i,1,m)*term1(i) + fint(i,2,m)*term2(i) + &
&          fint(i,3,m)*term3(i) + fint(i,4,m)*term4(i)
    END DO
  END DO

  RETURN
END SUBROUTINE lagyin
